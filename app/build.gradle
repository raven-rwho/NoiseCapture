apply plugin: 'com.android.application'

def getCheckedOutGitCommitHash() {
    def gitFolder = "$rootProject.projectDir/.git/"
    /*
     * '.git/HEAD' contains either
     *      in case of detached head: the currently checked out commit hash
     *      otherwise: a reference to a file containing the current commit hash
     */
    def head = new File(gitFolder + "HEAD").text.split(":") // .git/HEAD
    def isCommit = head.length == 1

    if (isCommit) return head[0].trim()

    def refHead = new File(gitFolder + head[1].trim()) // .git/refs/heads/master
    refHead.text.trim()
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'
    def signingFilePath = System.getProperty("user.home")+ "/.idea/signing.gradle"
    if(new File(signingFilePath).exists()) {
        apply from: signingFilePath
    }
    defaultConfig {
        applicationId "org.noise_planet.noisecapture"
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 22
        versionName "0.4.6 (Beta)"
        // Store build date in apk
        buildConfigField "long", "TIMESTAMP", System.currentTimeMillis() + "L"
        buildConfigField "String", "GITHASH", "\"${getCheckedOutGitCommitHash().toString()}\""
        // Enabling multidex support.
        multiDexEnabled false

        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }
    lintOptions {
        abortOnError false
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if(new File(signingFilePath).exists()) {
                signingConfig signingConfigs.release
            }
        }
        debug {
            debuggable true
            if(new File(signingFilePath).exists()) {
                signingConfig signingConfigs.debug
            }
        }
    }
}

// For using the MPAndroidChart package
// https://github.com/PhilJay/MPAndroidChart
// Apache License, Version 2.0

repositories {
    maven {
        url "https://jitpack.io"
    }
    maven {
        url "https//repo.orbisgis.org"
    }
    mavenLocal()
}

dependencies {
    compile 'com.github.PhilJay:MPAndroidChart:v2.2.5'
    compile 'org.slf4j:slf4j-simple:1.7.12'
    // multithreaded FFT for realtime visualisation of spectrum only
    compile 'com.github.wendykierp:JTransforms:3.1'
    compile 'org.apache.commons:commons-math3:3.5'
    compile 'com.android.support:appcompat-v7:25.1.0'
    compile 'com.android.support:design:25.1.0'
    compile 'com.android.support:support-vector-drawable:25.1.0'
    compile 'com.android.support:animated-vector-drawable:25.1.0'
    compile 'com.google.android.gms:play-services-maps:10.0.1'
    compile 'com.nhaarman.supertooltips:library:3.0.0'
    compile 'com.ullink.slack:simpleslackapi:0.6.0'
    //compile 'com.android.support:multidex:1.0.0'
    // Testing-only dependencies
//    // Force usage of support annotations in the test app, since it is internally used by the runner module.
//    androidTestCompile 'com.android.support:support-annotations:24.2.1'
//    androidTestCompile 'com.android.support.test:runner:0.4.1'
//    androidTestCompile 'com.android.support.test:rules:0.4.1'
//    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.1'
    // unit test
//    testCompile 'org.robolectric:robolectric:3.1.2'
//    testCompile 'junit:junit:4.12'
    compile project(':sosfilter')
}
